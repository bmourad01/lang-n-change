(* extend the grammar for references (assume that booleans are present in the source language) *)
Expression e ::= ... | ("address" [$a.]) | ("unit" []) | ("ref" [$e.]) | ("deref" [$e.]) | ("assign" [$e, $e]) | ("seq" [$e, $e]) | ("while" [$e, $e]).;
Value v ::= ... | ("address" [$a.]) | ("unit" []).;
Type T ::= ... | ("unitT" []) | ("refT" [$T.]).;
Context E ::= ... | ("ref" [$E.]) | ("deref" [$E.]) | ("assign" [$E, $e]) | ("assign" [$v, $E]) | ("seq" [$E, $e]).;
Memory m ::= [<[$a, $v]>...].;
Store Sigma ::= [<[$a, $T]>...].;

(* references are always invariant *)
# variance: ... | refT => inv;

(* modify the shape of the typing relation *)
add_relation(<[$Gamma, $Sigma]> |- $e : $T);

(* modify the shape of the step relation *)
add_relation(<[$e, $m]> --> <[$e, $m]>);

(* modify all typing judgements in rules to have the new shape *)
lift (gamma |- e : t) to (<[gamma, $Sigma]> |- e : t).;

(* modify the shape of reduction rules *)
lift (e1 --> e2) to (<[e1, $m]> --> <[e2, $m]>).;

(* any steps that appear in the premises of a reduction rule must
 * produce a fresh memory which is then used in the next step *)
(Rule(keep)[`<[e1, _]> --> <[e2, _]>`]:
  let step_prems = Premises[`_ --> _`]: self in
  let steps = step_prems @ [conclusion.] in
  let stores = steps[`_`]: fresh_var("m") in
  let smap1 = zip(steps, stores) in
  let smap2 = zip(step_prems, tail(stores)) in 

  [rule_name(self)] {
    Premises(keep)[`<[e1, _]> --> <[e2, _]>`]: <[e1, smap1(self)]> --> <[e2, smap2(self)]>
    --------------------------------------------------------------------------------------
    <[e1, head(stores)]> --> <[e2, smap1(conclusion)]>
  }
);

(* add the typing rules for the new top-level operators *)

let assume = head(relations[`("typeof", a :: _)`]: a) in

["T-ADDRESS"] {
  &member <[$a, $T]> $Sigma
  --------------------------------------------
  assume |- ("address" [$a.]) : ("refT" [$T.])
};

["T-UNIT"] {
  ------------------------------------
  assume |- ("unit" []) : ("unitT" [])
};

["T-REF"] {
  assume |- $e : $T
  ----------------------------------------
  assume |- ("ref" [$e.]) : ("refT" [$T.])
};

["T-DEREF"] {
  assume |- $e : ("refT" [$T.])
  ------------------------------
  assume |- ("deref" [$e.]) : $T
};

["T-ASSIGN"] {
  assume |- $e1 : ("refT" [$T.]),
  assume |- $e2 : $T
  ----------------------------------------------
  assume |- ("assign" [$e1, $e2]) : ("unitT" [])
};

["T-SEQ"] {
  assume |- $e1 : ("unitT" []),
  assume |- $e2 : $T
  ---------------------------------
  assume |- ("seq" [$e1, $e2]) : $T
};

["T-WHILE"] {
  assume |- $e1 : ("bool" []),
  assume |- $e2 : ("unitT" [])
  ---------------------------------------------
  assume |- ("while" [$e1, $e2]) : ("unitT" [])
};

(* add the reduction rules for the new top-level operators *)

["R-REFV"] {
  &($a = $fresh($dom($m)))
  ------------------------------------------------------------
  <[("ref" [$v.]), $m]> --> <[("address" [$a.]), [$a => $v]$m]>
};

["R-DEREF"] {
  &member <[$a, $v]> $m
  -----------------------------------------------------
  <[("deref" [("address" [$a.]).]), $m]> --> <[$v, $m]>
};

["R-ASSIGN"] {
  &member $a $dom($m)
  ----------------------------------------------------------------------------
  <[("assign" [("address" [$a.]), $v]), $m]> --> <[("unit" []), [$a => $v]$m]>
};

["R-SEQ"] {
  ------------------------------------------------
  <[("seq" [("unit" []), $e]), $m]> --> <[$e, $m]>
};

["R-WHILE"] {
  --------------------------------------------------------------------------------------------------------
  <[("while" [$e1, $e2]), $m]> --> <[("ite" [$e1, ("seq" [$e2, ("while" [$e1, $e2])]), ("unit" [])]), $m]>
};

(* add the relation to check the memory for well-formedness *)

add_relation("memory_ok", [$Sigma, $m]);

# mode: ... | memory_ok => inp inp;
# principal: ... | memory_ok => no yes;

["MEMORY-OK-NIL"] {
  ----------------------------
  &("memory_ok" [$Sigma, $nil])
};

let mem_assume = match assume with <_ :: ts> -> <$nil :: ts> in

["MEMORY-OK"] {
  mem_assume |- $v : $T,
  &member <[$a, $T]> $Sigma,
  &("memory_ok" [$Sigma, $m])
  ------------------------------------------
  &("memory_ok" [$Sigma, $(<[$a, $v]> :: $m)])
}
